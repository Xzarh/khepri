/**
 * @fileOverview Compile a Khepri file/directory to an ECMAScript file/directory.
 */
package compileFile
with
    import 'path' path,
    import 'fs' fs,
    
    import 'async' {each},
    
    import './compile' compile
in {

static console;

var KHEPRI_EXT := `^\.kep$`i;

var rename := \file ->
    path.basename(file, '.kep') + '.js';

/**
 * @param inFile Path to input file or directory.
 * @param [outFile] Path to output file or directory.
 * @param [header] {String, Function} Prefix for output. If a function, passed
 *   the inputFile name.
 * @param options Compiler options.
 * @param ok Success callback
 * @param err Error callback.
 */
compileFile := \inFile, outFile, header, options, ok, error ->
    fs.realpath(inFile, \err resolvedPath -> {
        if (err) throw err;
        
        // Walk sub directories and compile all `*.kep` files.
        if (fs.lstatSync(resolvedPath).isDirectory())
            return fs.readdir(resolvedPath, \err files -> {
                each @ files \ file -> {
                    var subPath = path.join(inFile, file);
                    
                    if (fs.lstatSync(subPath).isDirectory())
                        return compileFile(
                            subPath,
                            outFile && path.join(outFile, file),
                            header,
                            options,
                            ok,
                            error);
                    
                    if (path.extname(file).match(KHEPRI_EXT))
                        return compileFile(
                            subPath,
                            outFile && path.join(outFile, rename file),
                            header,
                            options,
                            ok,
                            error);
                };
            });
        
        fs.readFile(resolvedPath, 'utf8', \err data -> {
            if (err) throw err;
            
            if (outFile)
                console.log("Khepri'" + inFile + "' to:'" + outFile + "'");
            
            compile(
                data,
                ?typeof header === 'function'
                    :header(inFile)
                    :header,
                options,
                inFile,
                \data -> ok(data, inFile, outFile),
                \data -> error(data, inFile, outFile));
        });
    });

}