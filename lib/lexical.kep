/**
 * @fileOverview
 */
package (
    check)
with
    import 'khepri-ast::node' ast_node#{setUserData},
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::pattern' ast_pattern,
    import 'khepri-ast::value' ast_value,
    
    import 'neith::zipper' zipper,
    import 'neith::tree' tree,
    
    import 'khepri-ast-zipper' {khepriZipper},
    
    import 'bes::record' record,
    import 'bes::object' object,
    
    import './scope' {Scope}
in {

var map = Function.prototype.call.bind(Array.prototype.map);
var reduce = Function.prototype.call.bind(Array.prototype.reduce);
var reduceRight = Function.prototype.call.bind(Array.prototype.reduceRight);

/* Continuation
 ******************************************************************************/
var Tail = function \f s ok err =self-> {
    self.f = f;
    self.s = s;
    self.ok = ok;
    self.err = err;
};

var trampoline = \f -> {
    var value = f;
    while (value instanceof Tail)
        value = value.f(value.s, value.ok, value.err);
    return value;
};

/* State
 ******************************************************************************/
var State = record.declare(null, [
    'ctx',
    'realScope',
    'scope',
    'vars',
    'unique']);

State.addVar = \s id v ->
    s.setVars(object.setProperty(s.vars, id, v, true));

/*
 ******************************************************************************/
var ok = \x -> \s, ok, _ -> ok(x, s);

var error = \x -> \s, _, err -> err(x, s);

var bind = \p f ->
    \s ok err ->
        new Tail(p, s, \x, s -> f(x)(s, ok, err), err);

var next = \p n -> bind(p, \_ -> n);

var seqa = \arr -> reduceRight(arr, \p, c -> next(c, p), ok());

var seq = \args(...) -> seqa(args);

var extract = \s ok, _ -> ok(s, s);

var setState = \s -> \_ ok _ -> ok(s, s);

var modifyState = \f -> bind(extract, \s -> setState(f(s)));

var examineState = \f -> bind(extract, \s -> f(s));

var move = \op ->
    modifyState(\s ->
        State.setCtx(s, op(s.ctx)));

var examineScope = \f -> bind(extract, \s -> f(s.scope));

var examineRealScope = \f -> bind(extract, \s -> f(s.realScope));

var modifyScope = \f ->
    \s, ok, err -> let
        scope = f(s.scope),
        newState = State.setScope(s, scope)
    in ok(scope, newState);

var modifyRealScope = \f ->
    \s, ok, err -> let
        scope = f(s.realScope),
        newState = State.setRealScope(s, scope)
    in ok(scope, newState);

var setScope = \s -> modifyScope(\() -> s);

var setRealScope = \s -> modifyRealScope(\() -> s);

/* Operations
 ******************************************************************************/
var pass = ok();

var registerVar = \name ->
    bind(examineState(\s -> ok(s.unique)), \uid ->
        seq(
            modifyState(\s -> s.setUnique(uid + 1)),
            ok(uid)));

/**
 * Create a new lexical block for body.
 */
var block = \body(...) ->
    examineScope(\s ->
        seq(
            setScope(new Scope({}, s, s.mapping)),
            seqa(body),
            setScope(s)));
    
/**
 * Create a new lexical block for body.
 */
var emptyBlock = \body(...) ->
    examineScope(\s ->
        seq(
            setScope(new Scope({}, s, {})),
            seqa(body),
            setScope(s)));

/**
 * Create a new lexical block for body.
 */
var realBlock = \body(...) ->
    examineRealScope(\s ->
        seq(
            setRealScope(new Scope({}, s, {})),
            emptyBlock.apply(undefined, body),
            setRealScope(s)));

/**
 * Test to see if the current scope already contains a binding for id.
 */
var checkCanAddOwnBinding = \id, loc -> 
    examineScope <|\ s ->
        ?!s.hasOwnBinding(id)
            :pass
            :let
                start = loc && loc.start,
                binding = s.getBinding(id),
                end = binding.loc && binding.loc.start
            in
                error("'" + id + "' at:" + start + " already bound for scope from:" + end);

/**
 * 
 */
var hasBinding = \id, loc ->
    examineScope <|\ s ->
        ?s.hasBinding(id)
            :pass
            :error("Undeclared identifier:'" + id + "' at:" + loc);

/**
 * 
 */
var hasFreeBinding = \id, loc ->
    seq(
        hasBinding(id, loc),
        examineScope <|\ s ->
            let current = s.getBinding(id) in
                ?current.reserved
                    :error("Undeclared identifier:'" + id + "' at:" + loc)
                    :pass);

/**
 * 
 */
var checkCanAssign = \id, loc -> 
    examineScope <|\ s ->
        ?s.hasBinding(id)
            :let b = s.getBinding(id) in
                ?b.mutable
                    :pass
                    :error("Assign to immutable variable:'" + id + "' at:"+ loc)
            :pass;

/**
 * 
 */
var getUnusedId = \id, loc -> 
    examineRealScope <|\ s ->
        ok(?s.hasOwnBinding(id)
            :s.getUnusedId(id)
            :id);

/**
 * 
 */
var addMapping = \id, newId ->
    modifyScope(\s -> Scope.addMapping(s, id, newId));

/**
 * Adds a new mutable binding in the current scope. Does not perform any checks.
 */
var addMutableBinding = \id, loc -> 
    seq(
         modifyScope(\s -> Scope.addMutableBinding(s, id, loc)),
         modifyRealScope(\s -> Scope.addMutableBinding(s, id, loc)),
         addMapping(id, id));

/**
 * Adds a new immutable binding in the current scope. Does not perform any checks.
 */
var addImmutableBinding = \id, loc -> 
    seq(
         modifyScope(\s -> Scope.addImmutableBinding(s, id, loc)),
         modifyRealScope(\s -> Scope.addImmutableBinding(s, id, loc)),
         addMapping(id, id));

/**
 * Adds a new, and unique, mutable binding in the current scope. Does not perform any checks.
 */
var addUniqueMutableBinding = \id, loc ->
    seq(
        checkCanAddOwnBinding(id, loc),
        examineRealScope <|\ s ->
            ?s.hasOwnBinding(id)
                :let new_id = s.getUnusedId(id) in
                    seq(
                         addMutableBinding(id, loc),
                         addMutableBinding(new_id, loc),
                         addMapping(id, new_id))
                :addMutableBinding(id, loc));

/**
 * 
 */
var addMutableBindingChecked = \id, loc ->
    seq(
        checkCanAddOwnBinding(id, loc),
        addUniqueMutableBinding(id, loc));

var addImmutableBindingChecked = \id, loc ->
    seq(
        checkCanAddOwnBinding(id, loc),
        addImmutableBinding(id, loc));

var addUnusedImmutableBinding = \id, loc ->
    examineRealScope <|\ s ->
        ?s.hasOwnBinding(id)
            :let new_id = s.getUnusedId(id) in
                seq(
                    addImmutableBinding(id, loc),
                    addImmutableBinding(new_id, loc),
                    addMapping(id, new_id))
            :addImmutableBindingChecked(id, loc);

var addUniqueImmutableBinding = \id, loc ->
    seq(
        checkCanAddOwnBinding(id, loc),
        addUnusedImmutableBinding(id, loc));

var addReservedBinding = \id, loc -> 
    seq(
         modifyScope(\s -> Scope.addReservedBinding(s, id, loc)),
         modifyRealScope(\s -> Scope.addReservedBinding(s, id, loc)),
         addMapping(id, id));
    
/*
 ******************************************************************************/
var _check;

/*
 ******************************************************************************/
var child = \f edge ->
    seq(
        move tree.child@edge,
        f,
        move zipper.up);

var checkCtx = \node ->
    _check <| (node && tree.node(node));

var checkTop = \s ok err ->
    checkCtx(s.ctx)(s, ok, err);

var inspect = \f ->
    examineState(\s -> f(tree.node s.ctx));
    
var checkChild = child@checkTop;

var modifyNode = \f ->
    move tree.modifyNode@f;

var setNode = \x ->
    move tree.setNode@x;

var up = move zipper.up;

var down = move zipper.down;

var left = move zipper.left;

var right = move zipper.right;
    
/*
 ******************************************************************************/
var checks = {};

var addCheck = \type, check -> {
    if (Array.isArray(type))
        type.forEach <|\x -> addCheck(x, check);
    else
        checks.(type) = check;
};

// Program and Packages
addCheck@'Program' <|
    checkChild 'body';

addCheck@'PackageExports' <|
    checkChild 'exports';

addCheck@'PackageExport' <|
    inspect <|\ node ->
        addMutableBindingChecked(node.id.name, node.loc);

addCheck@'Package' <|
    seq(
        addImmutableBindingChecked('require', null),
        addImmutableBindingChecked('exports', null),
        addImmutableBindingChecked('module', null),
        checkChild 'exports',
        inspect <|\ node ->
            ?node.body.type === 'WithStatement'
                :child(
                    seq(
                        checkChild 'bindings',
                        child(checkChild 'body', 'body')),
                    'body')
                :child(checkChild 'body', 'body'));

// Clause
addCheck@'SwitchCase' <|
    seq(
        checkChild 'test',
        checkChild 'consequent');

addCheck@'CatchClause' <|
    block(
        inspect <|\ node ->
            addImmutableBindingChecked(node.param.name, node.param.loc),
        child(
            checkChild 'body',
            'body'));

// Declarations
addCheck@['StaticDeclaration', 'VariableDeclaration'] <|
    checkChild 'declarations';

addCheck@'StaticDeclarator' <|
    inspect <|\ node ->
        addImmutableBindingChecked(node.id.name, node.loc);
    
addCheck@'VariableDeclarator' <|
    seq(
        inspect <|\ node ->
            addMutableBindingChecked(node.id.name, node.loc),
        checkChild 'id',
        checkChild 'init');
    
addCheck@'Binding' <|
    seq(
        checkChild 'pattern',
        checkChild 'value');

// Statement
addCheck@'BlockStatement' <|
    block(
        checkChild 'body');

addCheck@'ExpressionStatement' <|
    checkChild 'expression';

addCheck@'IfStatement' <|
    seq(
        checkChild 'test',
        block(
            checkChild 'consequent'),
        block(
            checkChild 'alternate'));

addCheck@'WithStatement' <|
     block(
        checkChild 'bindings',
        child(checkChild 'body', 'body'));

addCheck@'SwitchStatement' <|
    block(
        checkChild 'discriminant',
        checkChild 'cases');

addCheck@['ReturnStatement', 'ThrowStatement'] <|
    checkChild 'argument';

addCheck@'TryStatement' <|
    seq(
        checkChild 'block',
        block(
            checkChild 'handler'),
        block(
            checkChild 'finalizer'));

addCheck@'WhileStatement' <|
    seq(
        checkChild 'test',
        block(
            checkChild 'body'));

addCheck@'DoWhileStatement' <|
    seq(
        block(
            checkChild 'body'),
        checkChild 'test');

addCheck@'ForStatement' <|
    block(
        checkChild 'init',
        checkChild 'test',
        checkChild 'update',
        block(
            checkChild 'body'));

// Expression
addCheck@'FunctionExpression' <|
    realBlock(
         inspect <|\ node ->
             ?node.id
                 :addImmutableBinding(node.id.name, node.loc)
                 :pass,
         checkChild 'params',
         inspect <|\ node ->
             ?node.body.type === "BlockStatement"
                 :child(checkChild 'body', 'body')
                 :checkChild 'body');

addCheck@'UnaryExpression' <|
    checkChild 'argument';

addCheck@'AssignmentExpression' <|
    seq(
        checkChild 'left',
        inspect <|\ node ->
            ?node.left.type === 'Identifier'
                :checkCanAssign(node.left.name, node.left.loc)
                :pass,
        checkChild 'right');

addCheck@['LogicalExpression','BinaryExpression'] <|
    seq(
        checkChild 'left',
        checkChild 'right');

addCheck@'ConditionalExpression' <|
    seq(
        checkChild 'test',
        checkChild 'consequent',
        checkChild 'alternate');

addCheck@['CallExpression', 'NewExpression'] <|
    seq(
        checkChild 'callee',
        checkChild 'args');

addCheck@'MemberExpression' <|
    seq(
        checkChild 'object',
        inspect <|\ node ->
            ?node.computed 
                :checkChild 'property'
                :pass);
    
addCheck@'ArrayExpression' <|
    checkChild 'elements';

addCheck@'ObjectExpression' <|
    checkChild 'properties';

addCheck@'LetExpression' <|
    block(
        checkChild 'bindings',
        checkChild 'body');

addCheck@'CurryExpression' <|
    seq(
        checkChild 'base',
        checkChild 'args');

// Patterns
addCheck@'SinkPattern'<|
    bind(
        getUnusedId('_'),
        \x -> seq(
            modifyNode <|\node -> {
                var n = setUserData(node, node.ud || {});
                var id = ast_value.Identifier.create(null, x);
                n.ud.id = id;
                return n;
            },
            inspect <|\ node -> addReservedBinding(x, node.loc)));

addCheck@'IdentifierPattern' <|
    inspect <|\ node -> {
        if (node.reserved)
            return addReservedBinding(node.id.name, node.loc);
        
        return seq(
            addUniqueImmutableBinding(node.id.name, node.loc),
            checkChild 'id',
            modifyNode(\node ->{
                var n = setUserData(node, node.ud || {});
                n.ud.id = ast_value.Identifier.create(null, node.id.name);
                return n;
            }));
    };
    
addCheck@'ImportPattern' <|
    checkChild 'pattern';

addCheck@'AsPattern' <|
    seq(
        checkChild 'id',
        inspect <|\ node -> child(
            seq(
               modifyNode(\target -> {
                    var n = setUserData(target, target.ud || {});
                    n.ud.id = node.id;
                    return n;
                }),
                checkTop),
            'target'));

addCheck@['ObjectPattern', 'ArrayPattern'] <|
    inspect <|\ node -> 
        examineScope(\s -> {
            if (!node.ud || !node.ud.id) {
                var unused = s.getUnusedId('__o');
                var id = ast_pattern.IdentifierPattern.create(node.loc,
                    ast_value.Identifier.create(null, unused));
                id.reserved = true;
                
                var n = setUserData(node, node.ud || {});
                n.ud.id = id;
                return seq(
                    setNode(ast_pattern.AsPattern.create(null, id, node)),
                    checkTop);
            }
            return checkChild 'elements';
        });

addCheck@'ObjectPatternElement' <|
    seq(
        checkChild 'target',
        checkChild 'key');

addCheck@'ArgumentsPattern' <|
    seq(
        checkChild 'id',
        checkChild 'elements',
        checkChild 'self');

// Value
addCheck@'ObjectValue' <|
    checkChild 'value';

addCheck@'Identifier' <|
    inspect <|\ node ->
        let name = node.name in examineScope <|\ s ->
            ?s.hasMapping(name)
                :let mappedName = s.getMapping(name) in
                    seq(
                        modifyNode(\x -> 
                            ast_node.modify(x, {}, {'name': mappedName})),
                        hasFreeBinding(mappedName, node.loc))
                :hasFreeBinding(name, node.loc);

/*
 ******************************************************************************/
_check = \node -> {
    if (Array.isArray(node)) {
        if (!node.length)
            return pass;
        
        return seq(
            down,
            seqa(map(node, \_ i -> 
                ?i === node.length - 1
                    :checkTop
                    :next(checkTop, right))),
            up);
    }
    
    if (node instanceof ast_node.Node && checks.(node.type))
        return checks.(node.type);
    
    return pass;
};


/**
 * 
 */
var checkAst = \ast globals -> {
    var scope = reduce(globals || [], Scope.addImmutableBinding, new Scope({}, null, {}));
    var state = new State(khepriZipper(ast), scope, scope);
    return trampoline(checkTop(state, \x s -> (s.ctx |> zipper.root |> tree.node), \err, s -> { throw err; }));
};

/**
 * 
 */
check = \ast globals ->
    checkAst(ast, globals);


}